vars = Variables()
vars.Add('pname', 'Set package name', 'basetest')
vars.Add('pversion', 'Set package version', '1.0.0')
env = Environment(tools=["default", "packaging"], variables = vars)
Help(vars.GenerateHelpText(env))
env.Prepend(RPM = 'TAR_OPTIONS="--transform \'s,^src/,,\' --transform \'s,SConscript,SConstruct,\'" ')
mma = File([ "bin/a.sh", "etc/c.conf", "man/m.txt" ])
env.Install("/var/tmp/tagtest", mma)
env.Tag(mma[0], UNIX_ATTR='(755, root, adm)')
env.Tag(mma[1], CONFIG=' ')
env.Tag(mma[2], DOC=' ')
env.Package(
    NAME=env['pname'],
    VERSION=env['pversion'],
    PACKAGEVERSION=0,
    PACKAGETYPE="rpm",
    LICENSE="gpl",
    SUMMARY="Check tags",
    DESCRIPTION="test basics",
    X_RPM_GROUP="build/tools",
)
env.Clean(mma, env['pname'] + '-' + env['pversion'])
